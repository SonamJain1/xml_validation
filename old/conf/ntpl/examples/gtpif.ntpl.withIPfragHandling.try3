# Key List IDs:
# 1 and 2 == Tenant IPs/SubNets

DefineMacro("TenatIPv4Addrs", "1")
DefineMacro("TenantIPv6Addrs", "2")
DefineMacro("GTPRxPort", "5")
DefineMacro("GTPTxPort", "6")

# Stream Ids
DefineMacro("Unfragmented_GTP_Stream", "1")
DefineMacro("IPv4_Fragment_Stream", "2")

Setup[TxDescriptor=Dyn;TxPorts=GTPTxPort;TxIgnorePos=42;UseWL=True] = StreamId == 1

# Tenant SubNets
IPMatchList[KeySet = TenatIPv4Addrs] = IPv4Addr == {[FF.FF.FF.0]:[103.1.85.0]}
IPMatchList[KeySet = TenantIPv6Addrs] = IPv6Addr == {[FFFF:0:0:0:0:0:0:0]:[2001:0618:0:0:0:0:0:0]}
#IPMatchList = IPv4Addr == {[FF.FF.FF.0]:[202.75.138.0]}
#IPMatchList = IPV4Addr == {[FF.FF.FF.0]:[203.126.1.0]}

Define IsIPv4 = Filter(Layer3Protocol == IPv4)
Define IsIPv6 = Filter(Layer3Protocol == IPv6)

Define IPv4Dst = Field(Layer3Header[16]/32)
Define IPv6Dst = Field(Layer3Header[24]/128)

DefineMacro("TowardsTenant", "((IsIPv4 AND KeyMatch(IPv4Dst) == TenatIPv4Addrs) OR \
                               (IsIPv6 AND KeyMatch(IPv6Dst) == TenantIPv6Addrs))")

DefineMacro("NotTowardsTenant", "((IsIPv4 AND KeyMatch(IPv4Dst) != TenatIPv4Addrs) OR \
                                  (IsIPv6 AND KeyMatch(IPv6Dst) != TenantIPv6Addrs))")

# Just dest port needs to be 3386
# according to section 9.1.1.1 of
# 3GPP TS 09.60 V7.10.0 (2002-12)

DefineMacro("IsGTPcV0", "((Data[DynOffset=DynOffUdpFrame; Offset=2; DataType=ByteStr2] == 3386) AND \
                          (Data[DynOffset=DynOffUdpData; Offset=1; DataType=ByteStr1] != 0xFF) AND \
                          (Data[DynOffset=DynOffUdpData; Offset=1; DataType=ByteStr1] != 26))")

DefineMacro("NotGTPcV0", "((Data[DynOffset=DynOffUdpFrame; Offset=2; DataType=ByteStr2] != 3386) OR \
                           (Data[DynOffset=DynOffUdpData; Offset=1; DataType=ByteStr1] == 0xFF) OR \
                           (Data[DynOffset=DynOffUdpData; Offset=1; DataType=ByteStr1] == 26))")

# This works for both IPv4 and IPv6 but we're only configuring
# IPv4 addresses for tenants at the mo.
DefineMacro("IsIPv4Fragment", "Fragment==First,Middle,Last")
DefineMacro("NotIPv4Fragment", "Fragment==Not")

# Offset0 = IPv4 header
# Offset1 = GTP packet start

# A filter can match against up to 2 KeyLists.
Assign[StreamId=Unfragmented_GTP_Stream;Descriptor=Dyn3,Offset0=Layer3Header[0],Offset1=Layer4Payload[0]] = Port == GTPRxPort AND \
                                                                                                            TowardsTenant AND \
                                                                                                            Layer4Protocol == UDP AND \
                                                                                                            NotIPv4Fragment AND \
                                                                                                            (TunnelType == GTPv1v2-C OR IsGTPcV0)

Assign[StreamId=IPv4_Fragment_Stream;Descriptor=Dyn3,Offset0=Layer3Header[0]] = Port == GTPRxPort AND \
                                                                                TowardsTenant AND \
                                                                                IsIPv4Fragment

Retransmit [SourcePort = GTPRxPort; DestinationPort = GTPTxPort] = NotTowardsTenant OR \
                                                                     (Layer4Protocol != UDP AND \
                                                                      NotIPv4Fragment) OR \
                                                                        (TunnelType != GTPv1v2-C AND \
                                                                         NotGTPcV0 AND \
                                                                         NotIPv4Fragment)
